buildscript {
    repositories {
        jcenter()
        google()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.24.4'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.15.0'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.github.ben-manes.versions'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    google()
}


android {
    compileSdkVersion 26

    defaultConfig {
        applicationId "org.toulibre.capitoledulibre"
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 10
        versionName "1.4.0"
        // Supported languages
        resConfigs "fr"
        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        def keystorePass
        def aliasPass
        if (System.getenv("TRAVIS")) {
            keystorePass = System.getenv("KEYSTORE_PASS")
            aliasPass = System.getenv("ALIAS_PASS")
        } else {
            Properties sProps = new Properties()
            sProps.load(rootProject.file('signing.properties').newDataInputStream())
            keystorePass = sProps.getProperty("KEYSTORE_PASS")
            aliasPass = sProps.getProperty("ALIAS_PASS")
        }
        release {
            storeFile rootProject.file('release.jks')
            storePassword keystorePass
            keyAlias 'release'
            keyPassword aliasPass
        }
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            // Beta
            ext.betaDistributionGroupAliases = "testeurs"
        }
    }

    aaptOptions {
        cruncherEnabled false
    }
}

dependencies {
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support:recyclerview-v7:26.1.0'
    implementation 'com.android.support:cardview-v7:26.1.0'
    implementation 'com.android.support:customtabs:26.1.0'
    implementation 'com.github.chrisbanes.photoview:library:1.2.4'
    implementation('com.crashlytics.sdk.android:crashlytics:2.6.5@aar') {
        transitive = true;
    }
    implementation 'com.squareup.okhttp3:okhttp:3.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.0'
    debugImplementation 'com.facebook.stetho:stetho:1.5.0'
    debugImplementation 'com.facebook.stetho:stetho-okhttp3:1.5.0'
    releaseImplementation project(':stetho-noop')
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.1'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'
}
