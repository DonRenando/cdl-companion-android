buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.13.0'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.github.ben-manes.versions'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}


android {
    compileSdkVersion 25
    buildToolsVersion "25.0.0"

    defaultConfig {
        applicationId "org.toulibre.capitoledulibre"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 9
        versionName "1.4.0"
    }

    signingConfigs {
        def keystorePass
        def aliasPass
        if (System.getenv("TRAVIS")) {
            keystorePass = System.getenv("KEYSTORE_PASS")
            aliasPass = System.getenv("ALIAS_PASS")
        } else {
            Properties sProps = new Properties()
            sProps.load(rootProject.file('signing.properties').newDataInputStream())
            keystorePass = sProps.getProperty("KEYSTORE_PASS")
            aliasPass = sProps.getProperty("ALIAS_PASS")
        }
        release {
            storeFile rootProject.file('release.jks')
            storePassword keystorePass
            keyAlias 'release'
            keyPassword aliasPass
        }
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            // Beta
            ext.betaDistributionGroupAliases = "testeurs"
        }
    }

    aaptOptions {
        cruncherEnabled false
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:25.0.0'
    compile 'com.android.support:cardview-v7:25.0.0'
    compile 'com.android.support:customtabs:25.0.0'
    compile 'com.github.chrisbanes.photoview:library:1.2.4'
    compile('com.crashlytics.sdk.android:crashlytics:2.6.5@aar') {
        transitive = true;
    }
    compile 'com.squareup.okhttp3:okhttp:3.4.2'
    compile 'com.squareup.okhttp3:logging-interceptor:3.4.2'
    debugCompile 'com.facebook.stetho:stetho:1.4.1'
    debugCompile 'com.facebook.stetho:stetho-okhttp3:1.4.1'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'
}
